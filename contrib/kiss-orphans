#!/usr/bin/awk -f
# List orphaned packages

BEGIN {
    # Base installation packages which should never be considered
    # orphaned.
    base = "baseinit baselayout gcc e2fsprogs musl " \
           "make busybox bzip2 grub kiss git"

    kissdb = ENVIRON["KISS_ROOT"] "/var/db/kiss"

    # Read installed packages.
    cmd = sprintf("cd '%s/installed'; printf '%%s\\n' *", kissdb)
    while (cmd | getline pkg == 1) {
        # Create empty array element.
        installed[pkg]
        orphans[pkg]
    }

    # Read dependency files of all installed packages.
    for (pkg in installed) {
        dep_file = kissdb "/installed/" pkg "/depends"

        while (getline < dep_file == 1) {
            # Normal dependency.
            if ($0 in installed)
                delete orphans[$0]

            # Build-time dependency.
            else if ($1 in orphans)
                orphans[$1] = " " $2
        }
        close(dep_file)
    }

    for (pkg in orphans) {
        # Exclude base installation packages.
        if (index(" " base " ", " " pkg " "))
            continue

        print pkg orphans[pkg]
    }
}
